//*** utility functions shared by event functions

// returns a props list where x and y are in relation to the app's x y
// and the look is either as supplied or as default for element.
// used by all elements
dynamic_props = { element look ->
  <-(if (is (type state) "string" ){
    <- (list 0 0 "")
  }{
    xy = (get state APP_XY)
    props = (get element 0)

    x = (add (get xy 0) (get props 0))
    y = (add (get xy 1) (get props 1))

    <- (if (or
      (less_than (get (get state APP_XY) X) 0)
      (less_than (get (get state APP_XY) Y) 0)
    ) {
      <- (list x y "")
    } {
      <- (if (is look void) {
        <- (list x y (get (get element 0) 2))
      } {
        <- (list x y look)
      })
    })
  })
}

keys = (list
  (list "Q" "W" "E" "R" "T" "Y" "U" "I" "O" "P")
  (list "A" "S" "D" "F" "G" "H" "J" "K" "L")
  (list "Z" "X" "C" "V" "B" "N" "M" )
)

to_uppercase = { key -> 
  lowercase = "abcdefghijklmnopqrstuvwxyz"
  uppercase = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"

  index = (find lowercase key)
  <- (if (is index void){
    <- key
  }{
    <- (get uppercase index) 
  })
}

to_lowercase = { key -> 
  lowercase = "abcdefghijklmnopqrstuvwxyz"
  uppercase = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"

  index = (find uppercase key)
  <- (if (is index void){
    <- key
  }{
    <- (get lowercase index) 
  })
}

// game rules
set_background = { letter_status item ->
  <- (if (is letter_status 2) {
    // green
    <- (background 2 item)
  } (is letter_status 1) {
    // yellow
    <- (background 3 item)
  } {
    <- (background 8 item)
  })
}

get_letter_status = { letter pos answer ->
  <- (if (is pos void) {
    <- 0
  } {
    <- (if (is (get answer pos) letter) {
      // green
      <- 2
    } (not (is (find answer letter) void)) {
      // yellow
      <- 1
    } {
      <- 0
    })
  })
}

is_valid_key = { keypress_name ->
  letters = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"

  <- (not (is (find letters keypress_name) void))
}

mutaute_state_on_input = { state keypress_name ->
  <- (if (is (get state INPLAY) 6) {
    // TODO end game things
    <- state
  } {
    // TODO: Check win
    line_in_play = (get (get state STATUS) (get state INPLAY))
    index = (find line_in_play " ")
    uppercase_keypress_name = (to_uppercase keypress_name)

    <- (if (and (is_valid_key uppercase_keypress_name) (not (is index void))){
      // inserting letter
      typed_in = (set line_in_play uppercase_keypress_name index)
      status = (set (get state STATUS) typed_in (get state INPLAY))
      
      <- (set state status STATUS)
    } (and (is uppercase_keypress_name  "\n") (is (find line_in_play " ") void)) {
      // next line
      // check word is valid
      word = (reduce line_in_play { accum item index -> <- (join accum item) } "")
      <- (if (is (find valid_words word) void) {
        // not valid
        // TODO: animate
        <- state
      } {
        used = (reduce line_in_play { accum item index ->
          <- (if (is (find accum item) void) { <- (insert accum item) } { <- accum })
        } (get state USED))

        state = (set state used USED)
        <- (set state (add (get state INPLAY) 1) INPLAY)
      })
    } (and (is uppercase_keypress_name  "delete") (not (is (get line_in_play 0) " "))) {
      // deleting letter
      typed_in = (if (not (is index void)) {
        <- (set line_in_play " " (subtract index 1))
      } {
        <- (set line_in_play " " 4)
      } )
      status = (set (get state STATUS) typed_in (get state INPLAY))
      <- (set state status STATUS)
    } {
      // any key not part of the game
      <- state
    })
  })
}

//***  settings
app_width = 80
app_height = 24

//***  elements
centered_modal = ({
  props = (list 0 0 "")

  on_state = {
    (if (or
      (less_than (get (get state APP_XY) X) 0)
      (less_than (get (get state APP_XY) Y) 0)
    ) {
      look = (map (range (subtract (rows) 0)) { _ index ->
        <- (if (is index (integer (divide (rows) 2))) {
          r = (map (range (subtract (columns) 0)) { _ _ -> <- " " })
          point = (subtract (integer (divide (columns) 2)) 12)
          point = (if (less_than point 0) {
            <- 0
          }{
            <- point
          })
          <- (set r "Enlarge Terminal Window" point)
        } {
          <- (map (range (subtract (columns) 0)) { _ _ ->
            <- " "
          })
        })
      })

      (draw (list 0 0 look))
    } {
      (draw (list 0 0 ""))
    })

    <- state
  }

  <- (list void (list on_state void void void void))
})

// utility element with no visible look.
// clears screen on resize, handles app exit, "mouse out" of pickers.
screen = ({
  props = (list 
    0
    0
    "................................................................................"
  )

  on_loop = {
    x = (integer (divide (subtract (columns) app_width) 2)) // center position on x
    y = (integer (divide (subtract (rows) app_height) 2))// center position on y

    curr = (list x y)

    <- (if (is curr (get state APP_XY)){
      <- state
    } {
      (go_to 0 2)
      (clear_below)

      <- (set state (list x y) APP_XY)
    })
  }

  on_state = {
    (draw (dynamic_props screen void))
    <- state
  }

  <- (list props (list on_state on_loop void void void))
})


create_element_key = { x y letter ->
  props = (list
    (add x 40)
    (add y 12)
    (list 
      (list "┌""─""┐")
      (list "│" letter "│")
      (list "└""─""┘")
    )
  )

  on_state = {
    element = (get elements listener_index)
    props = (get element 0)
    look = (get props 2)
    letter = (get (get look 1) 1)


    look = (if (is (find (get state USED) letter) void) {
      <- look
    } {
      final_letter_status = (reduce (get state STATUS) {accum word_line index ->
        // only change key look after word is submitted
        <- (if (less_than index (get state INPLAY)) {
          guessed_pos = (find word_line letter)
          letter_status = (get_letter_status letter guessed_pos (get state ANSWER))

          <- (if (greater_than letter_status accum){
            <- letter_status
          }{
            <- accum
          })
        }{
          <- accum
        })
      } 0)

      <- (set_background final_letter_status look)
    })

    (draw (dynamic_props element look))

    <- state
  }

  on_click = {
    element = (get elements listener_index)
    props = (dynamic_props element void)

    <- (if (is (mouse_over props mouse_xy) 1) {
      look = (get props 2)
      letter = (get (get look 1) 1)

      <- (mutaute_state_on_input state letter)
    } {
      <- state
    })

    <- state
  }

  <- (list props (list on_state void void void on_click))
}

enter_key = ({
  props = (list
    40
    18
    (list 
      (list "┌""─""─""─""┐")
      (list "│"" ""↩"" ""│")
      (list "└""─""─""─""┘")
    )
  )

  on_state = {
    (draw (dynamic_props enter_key void))
    <- state
  }

  on_click = {
    props = (dynamic_props enter_key void)

    <- (if (is (mouse_over props mouse_xy) 1) {
      <- (mutaute_state_on_input state "\n")
    } {
      <- state
    })

    <- state
  }

  <- (list props (list on_state void void void on_click))
})

delete_key = ({
  props = (list
    66
    18
    (list 
      (list "┌""─""─""┐")
      (list "│"" ""⌫""│")
      (list "└""─""─""┘")
    )
  )

  on_state = {
    (draw (dynamic_props delete_key void))
    <- state
  }

  on_click = {
    props = (dynamic_props delete_key void)

    <- (if (is (mouse_over props mouse_xy) 1) {
      <- (mutaute_state_on_input state "delete")
    } {
      <- state
    })

    <- state
  }

  <- (list props (list on_state void void void on_click))
})

board = ({
  props = (list
    9
    3
    (list 
      (list "┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐")
      (list "│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│")
      (list "└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘")
      (list "┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐")
      (list "│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│")
      (list "└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘")
      (list "┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐")
      (list "│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│")
      (list "└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘")
      (list "┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐")
      (list "│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│")
      (list "└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘")
      (list "┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐")
      (list "│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│")
      (list "└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘")
      (list "┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐""┌""─""─""─""┐")
      (list "│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│""│"" "" "" ""│")
      (list "└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘""└""─""─""─""┘")
    )
  )

  on_keypress = {
    <- (mutaute_state_on_input state keypress_name)
  }

  on_state = {
    props = (get board 0)

    mapped = (map (get props 2) {row row_index -> 
      <- (map row { item index ->
        r =  (integer (divide row_index 3))
        c =  (integer (divide index 5))
        letter = (get (get (get state STATUS) r) c)

        <- (if (and (is (remainder row_index 3) 1) (is (remainder index 5) 2) ) {
          <- (if (is (get state INPLAY) r) {
              <- (if (is letter " ") {
                <- letter
              } {
                <- letter
              })
          }{
            <- (if (is letter " ") {
              <- letter
            } {
              letter_status = (get_letter_status letter c (get state ANSWER))
              <- (set_background letter_status letter)
            })
          })
        }{
          <- (if (is (get state INPLAY) r) {
            <- item
          }{
            <- (if (is letter " ") {
              <- item
            } {
              letter_status = (get_letter_status letter c (get state ANSWER))
              <- (set_background letter_status item)
            })
          })
        })
      })
    })

    (draw (dynamic_props board mapped))

    <- state
  }

  <- (list props (list on_state void on_keypress void void))
})

answer = ({
  props = (list
    53
    3
    "?????"
  )

  on_state = {
    (if (is (get state INPLAY) 6) {
      (draw (dynamic_props answer (get state ANSWER)))
    } {
      (draw (dynamic_props answer void))
    })

    <- state
  }

  <- (list props (list on_state void void void void))
})

kb = (reduce keys { line_accum key_line line_index ->
  <- (reduce key_line { accum item index -> 
    <- (insert accum (create_element_key (add (multiply line_index 2) (integer (divide line_index 2)) (multiply index 3)) (multiply line_index 3) item)) 
  } line_accum)
} (list))

kb = (insert kb enter_key)
kb = (insert kb delete_key)

el = (list 
  screen 
  centered_modal
  board
  answer
)

elements = (join kb el)
